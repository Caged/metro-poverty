#!/usr/bin/env coffee

argv   = require('minimist')(process.argv.slice(2))
carto  = require 'carto'
mapnik = require 'mapnik'
path   = require 'path'
fs     = require 'fs'
pgsync = require 'pg-sync'

project = argv.p or argv.project
database = argv.d or argv.database or 'metro_poverty'
cbsa = argv.c or argv.cbsa

if !project? or !database? or !cbsa
  return console.log("Usage: #{path.basename(__filename)} --project path/to/project.mml --database database_name --cbsa metro_cbsa")

console.time('render')

client = new pgsync.Client()
client.connect("dbname=#{database}")

projectsPath = path.join __dirname, '../', 'data', 'mml'
styles = []
target = path.join projectsPath, "#{project}.mml"
mml    = JSON.parse(fs.readFileSync target, 'utf-8')

query  = "select st_asgeojson(st_extent(st_transform(geom, 2163))) as extent from tracts inner join tract_data on tract_data.tractid = tracts.geoid10 where cbsa_num ='#{cbsa}'"
results = client.query(query)
extent = JSON.parse(results[0].extent)
bounds = extent.coordinates[0][0].concat(extent.coordinates[0][1])

# 1419060.3114986,
#   -1134121.79766478,
#   1419060.3114986,
#   -1096014.19104458

# mml.bounds = [-1134121.79766478, -1096014.19104458, 1419060.3114986, 1419060.3114986]
console.log bounds
mml.Layer.push
  name: 'tracts'
  Datasource:
    type: 'postgis'
    host: 'localhost'
    dbname: database
    table: "(select gid, st_transform(geom, 2163) as geom from tracts inner join tract_data on tract_data.tractid = tracts.geoid10 where cbsa_num = '#{cbsa}') as tracts"
    extent: bounds

#
# return

# Carto doesn't do this transform automatically, so we need to go through
# each stylesheet and read it from the file system, and convert Stylesheet to
# {id, data} objects
mml.Stylesheet.forEach (style) ->
  styles.push
    id: path.basename style
    data: fs.readFileSync path.join(__dirname, '../', 'data', 'mss', style), 'utf-8'

mml.Stylesheet = styles
out = new carto.Renderer().render(mml)

mapnik.register_system_fonts()
mapnik.register_default_input_plugins() if mapnik.register_default_input_plugins

map = new mapnik.Map 10240, 5760
map.fromStringSync out
map.zoomAll()
map.renderFileSync path.join(__dirname, '../', 'data', 'png', "#{project}.png")
console.timeEnd('render')
